import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    // todo: Add unavailable dependencies to local lib folder
    compile 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compile 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:VaultAPI:1.6'
    compile 'me.clip:placeholderapi:2.8.4'
    compile 'com.sk89q:worldguard:6.1.1-SNAPSHOT'
    compile 'net.ess3:EssentialsX:2.0.1'
    compile 'net.minelink:CombatTagPlus:1.3.1'
    compile(group: 'com.massivecraft', name: 'Factions', version: '1.6.9.5-U0.1.14') {
        exclude(module: 'fanciful')
    }
    compile 'org.projectlombok:lombok:1.16.20'

    compile project(':duels-api')
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'be.maximvdw:MVdWPlaceholderAPI:2.5.1-SNAPSHOT'
    compile 'org.inventivetalent.spiget-update:bukkit:1.4.0'
}

shadowJar {
    archiveName = parent.name + '-' + project.version + '.jar'
    // todo: remove later
    exclude "me/realized/_duels/**"

    dependencies {
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('be.maximvdw:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }
    exclude 'LICENSE*'
    relocate 'org.inventivetalent.update.spiget', project.group.toString() + '.shaded.update.spiget'
    relocate 'com.google.gson', project.group.toString() + '.shaded.gson'
}

build.dependsOn(shadowJar)